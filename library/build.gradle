task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.1'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'

dependencies {
    compile 'com.android.support:support-v4:18.0.0'
}

android {
  compileSdkVersion 14
  buildToolsVersion '17.0.0'

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      res.srcDirs = ['res']
    }
  }
}

def deployToProd = hasProperty("pathMaven.deployToProd") && project.ext["pathMaven.deployToProd"].toBoolean()
println "will deploy to prod?" + deployToProd

uploadArchives {
    repositories {
        mavenDeployer {
            if(deployToProd) {
                snapshotRepository(url: project.ext["pathMaven.repo.snapshots"]) {
                    authentication(userName: project.ext["pathMaven.username"], password: project.ext["pathMaven.password"]);
                }

                repository(url: project.ext["pathMaven.repo.releases"]) {
                    authentication(userName: project.ext["pathMaven.username"], password: project.ext["pathMaven.password"]);
                }
            } else {
                repository(url: mavenLocal().getUrl())
            }

            pom.project {
                groupId 'com.actionbarsherlock'
                artifactId 'parent'
                version '4.4.0.5'
            }
        }
    }
}
